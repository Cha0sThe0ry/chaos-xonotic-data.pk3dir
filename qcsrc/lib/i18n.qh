#ifndef I18N_H
#define I18N_H

#include "log.qh"
#include "unsafe.qh"

// translation helpers
string prvm_language;

string language_filename(string s)
{
	string fn = prvm_language;
	if (fn == "" || fn == "dump") return s;
	fn = strcat(s, ".", fn);
	int fh = fopen(fn, FILE_READ);
	if (fh >= 0)
	{
		fclose(fh);
		return fn;
	}
	return s;
}

#ifndef CTX_CACHE
	#define CTX_CACHE 1
#endif

#if CTX_CACHE
	AL_declare(CTX_cache);
	STATIC_INIT(CTX_cache)
	{
		AL_init(CTX_cache, 0, string_null, s);
	}
	SHUTDOWN(CTX_cache)
	{
		AL_delete(CTX_cache);
	}
#endif

string CTX(string s)
{
#if CTX_CACHE
		int i = strid(s);
		string c = AL_gets(CTX_cache, i);
		if (c) return c;
#endif
	int p = strstrofs(s, "^", 0);
	string ret = (p < 0) ? s : substring(s, p + 1, -1);
#if CTX_CACHE
        LOG_DEBUGF("CTX(\"%s\")\n", s);
		AL_sets(CTX_cache, i, ret);
#endif
	return ret;
}

#define ZCTX(s) strzone(CTX(s))

#endif
