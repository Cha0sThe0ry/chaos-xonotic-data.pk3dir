#pragma once

#ifndef MENUQC
.bool debug;
.int sv_entnum;
REGISTER_NET_TEMP(net_debug)
#endif

#ifdef SVQC

#include <server/command/all.qh>

#endif

#ifdef CSQC
	NET_HANDLE(net_debug, bool isNew)
	{
		Net_Accept(net_debug);
		this.sv_entnum = ReadShort();
		if (ReadByte()) make_pure(this);
		this.origin_x = ReadCoord();
		this.origin_y = ReadCoord();
		this.origin_z = ReadCoord();
		setorigin(this, this.origin);
		this.debug = true;  // identify server entities by this
		this.classname = strzone(ReadString());
		this.sourceLoc = strzone(ReadString());
		return true;
	}
#endif

#ifdef SVQC
	bool debug_send(entity this, entity to, int sf)
	{
		int channel = MSG_ONE;
		msg_entity = to;
		WriteHeader(channel, net_debug);
		WriteShort(channel, etof(this));
		WriteByte(channel, is_pure(this));
		vector o = this.origin;
		if (o == '0 0 0') // brushes
			o = (this.absmin + this.absmax) / 2;
		if (this.tag_entity)
			o += this.tag_entity.origin;
		WriteCoord(channel, o.x); WriteCoord(channel, o.y); WriteCoord(channel, o.z);
		WriteString(channel, this.classname);
		WriteString(channel, this.sourceLoc);
		return true;
	}
#endif

#ifndef MENUQC
/**
 * 0: off
 * 1: on
 * 2: on (pure)
 * 3: on (.entnum != 0)
 * 4: on (.origin == '0 0 0')
 * 5: on (.debug != 0), server only
 * 6: on (.solid != 0)
 */
bool autocvar_debugdraw;
#endif

#ifdef CSQC
	string autocvar_debugdraw_filter, autocvar_debugdraw_filterout;
	.int debugdraw_last;
	vector project_3d_to_2d(vector vec);
	void Debug_Draw()
	{
		if (!autocvar_debugdraw) return;
		static int debugdraw_frame;
		++debugdraw_frame;
		const int sz = 8;
		FOREACH_ENTITY(true, LAMBDA(
			if (it.debugdraw_last == debugdraw_frame) continue;
			int ofs = 0;
			for (entity e = findradius(it.origin, 100); e; e = e.chain)
			{
				if (e.debugdraw_last == debugdraw_frame) continue;
				e.debugdraw_last = debugdraw_frame;
				vector rgb = (e.debug) ? '0 0 1' : '1 0 0';
				if (autocvar_debugdraw_filterout != "" && strhasword(autocvar_debugdraw_filterout, e.classname)) continue;
				if (autocvar_debugdraw_filter != "" && !strhasword(autocvar_debugdraw_filter, e.classname)) continue;
				if (autocvar_debugdraw == 3)
				{
					if (!e.entnum) continue;
				}
				if (autocvar_debugdraw == 4)
				{
					if (e.origin) continue;
				}
				if (autocvar_debugdraw == 5)
				{
					if (!e.debug) continue;
				}
				else if (autocvar_debugdraw > 5)
				{
					bool flag = true;
					do {
//						if (e.modelindex) break;
//						if (e.absmin) break;
//						if (e.absmax) break;
//						if (e.entnum) break;
//						if (e.drawmask) break;
//						if (e.predraw) break;
//						if (e.movetype) break;
						if (e.solid) break;
//						if (e.origin) break;
//						if (e.oldorigin) break;
//						if (e.velocity) break;
//						if (e.angles) break;
//						if (e.avelocity) break;
//						if (e.classname) break;
//						if (e.model) break;
//						if (e.frame) break;
//						if (e.skin) break;
//						if (e.effects) break;
//						if (e.mins) break;
//						if (e.maxs) break;
//						if (e.size) break;
//						if (e.touch) break;
//						if (e.use) break;
//						if (e.think) break;
//						if (e.blocked) break;
//						if (e.nextthink) break;
//						if (e.chain) break;
//						if (e.netname) break;
//						if (e.enemy) break;
//						if (e.flags) break;
//						if (e.colormap) break;
//						if (e.owner) break;
						flag = false;
					} while (0);
					if (!flag) continue;
				}
				else if (is_pure(e))
				{
					if (autocvar_debugdraw < 2) continue;
					rgb.y = 1;
				}
				vector o = e.origin;
				if (e.tag_entity)
					o += e.tag_entity.origin;
				vector pos = project_3d_to_2d(o);
				if (pos.z < 0) continue;
				pos.z = 0;
				pos.y += ofs * sz;
				drawcolorcodedstring2(pos,
					sprintf("%d: '%s'@%s", (e.debug ? e.sv_entnum : etof(e)),
					e.classname, e.sourceLoc),
					sz * '1 1 0', rgb, 0.5, DRAWFLAG_NORMAL);
				++ofs;
			}
		));
	}
#endif

#ifdef SVQC
	COMMON_COMMAND(debugdraw_sv, "Dump all server entities")
	{
		switch (request)
		{
			case CMD_REQUEST_COMMAND:
			{
				if (!autocvar_debugdraw) return;
				int n = 1000;
				int rem = n;
				for (entity e = NULL; (e = findfloat(e, debug, 0)) && rem > 0; )
				{
					if (autocvar_debugdraw < 2 && is_pure(e)) continue;
					debug_send(e, caller, 0);
					e.debug = true;
					--rem;
				}
				LOG_INFOF("%d server entities sent\n", n - rem);
				return;
			}

			default:
			case CMD_REQUEST_USAGE:
			{
				LOG_INFO(strcat("\nUsage:^3 ", GetProgramCommandPrefix(), " debugdraw_sv"));
				return;
			}
		}
	}
#endif

GENERIC_COMMAND(bufstr_get, "Examine a string buffer object")
{
	switch (request)
	{
		case CMD_REQUEST_COMMAND:
		{
			int bufhandle = stof(argv(1));
			int string_index = stof(argv(2));
			string s = bufstr_get(bufhandle, string_index);
			LOG_INFOF("%s\n", s);
			return;
		}

		default:
		case CMD_REQUEST_USAGE:
		{
			LOG_INFO(strcat("\nUsage:^3 ", GetProgramCommandPrefix(), " bufstr_get bufhandle string_index"));
			return;
		}
	}
}

GENERIC_COMMAND(version, "Print the current version")
{
	switch (request)
	{
		case CMD_REQUEST_COMMAND:
		{
			LOG_INFO(WATERMARK "\n");
			return;
		}
		default:
		case CMD_REQUEST_USAGE:
		{
			LOG_INFO(strcat("\nUsage:^3 ", GetProgramCommandPrefix(), " version"));
			return;
		}
	}
}

REGISTER_STAT(TRACE_ENT, int)
#ifdef SVQC
bool autocvar_debugtrace;

REGISTER_MUTATOR(trace, autocvar_debugtrace);

.bool debug_trace_button;
.int solid_prev;
MUTATOR_HOOKFUNCTION(trace, SV_StartFrame)
{
	FOREACH_CLIENT(true, {
		bool skip = false;
		bool btn = PHYS_INPUT_BUTTON_HOOK(it);
		if (btn == it.debug_trace_button) skip = true;
		it.debug_trace_button = btn;
		if (!btn || skip) continue;
		FOREACH_ENTITY(true, {
		    it.solid_prev = it.solid;
			it.solid = SOLID_BBOX;
		});
		vector forward; vector right; vector up;
		MAKEVECTORS(makevectors, it.v_angle, forward, right, up);
		vector pos = it.origin + it.view_ofs;
		traceline(pos, pos + forward * MAX_SHOT_DISTANCE, MOVE_NORMAL, it);
		FOREACH_ENTITY(true, {
		    it.solid = it.solid_prev;
            it.solid_prev = 0;
		});
		entity e = trace_ent;
		int i = etof(e);
		STAT(TRACE_ENT, it) = i;
		if (!e) continue;
		setorigin(e, e.origin + '0 0 100');
		stuffcmd(it, sprintf("prvm_edict server %d\n", i));
	});
}
#endif
#ifdef CSQC
entity TRACE_ENT;
void Trace_draw2d(entity this)
{
	int e = STAT(TRACE_ENT);
	if (!e) return;
	vector pos = '0 0 0';
	pos.y += vid_conheight / 2;
	drawstring(pos, sprintf("prvm_edict server %d", e), '10 10 0', '1 1 1', 1, DRAWFLAG_NORMAL);
}

STATIC_INIT(TRACE_ENT)
{
	entity e = TRACE_ENT = new_pure(TRACE_ENT);
	e.draw2d = Trace_draw2d;
}
#endif

#ifdef SVQC
SERVER_COMMAND(find, "Search through entities for matching classname")
{
	switch (request)
	{
		case CMD_REQUEST_COMMAND:
		{
			for (entity client = NULL; (client = find(client, classname, argv(1))); )
				LOG_INFO(etos(client), "\n");
			return;
		}

		default:
			LOG_INFO("Incorrect parameters for ^2find^7\n");
		case CMD_REQUEST_USAGE:
		{
			LOG_INFO("\nUsage:^3 sv_cmd find classname\n");
			LOG_INFO("  Where 'classname' is the classname to search for.\n");
			return;
		}
	}
}

SERVER_COMMAND(findat, "Search through entities for matching origin")
{
	switch (request)
	{
		case CMD_REQUEST_COMMAND:
		{
		    vector match = stov(argv(1));
		    FOREACH_ENTITY_ORDERED(it.origin == match, LAMBDA(LOG_INFOF("%i\n", it)));
			return;
		}

		default:
			LOG_INFO("Incorrect parameters for ^2findat^7\n");
		case CMD_REQUEST_USAGE:
		{
			LOG_INFO("\nUsage:^3 sv_cmd findat \"0 0 0\"\n");
			return;
		}
	}
}
#endif
